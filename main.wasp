app socialpostgpt {
  wasp: {
    version: "^0.13.0"
  },
  head: [
    "<script defer data-domain=\"socialpostgpt.xyz\" src=\"http://plausible.apps.twoducks.dev/js/script.js\"></script>"
  ],
  title: "SocialPostGPT",
  client: {
    rootComponent: import { App } from "@src/App.tsx",
  },
  db: {
    system: PostgreSQL,
  },
}

route RootRoute { path: "/", to: MainPage }
page MainPage {
  component: import Main from "@src/pages/MainPage.tsx"
}

route ResultPage { path: "/:generationId", to: ResultPage }
page ResultPage {
  component: import Main from "@src/pages/ResultPage.tsx"
}

action submitPrompt {
  fn: import { submitPrompt } from "@src/generation.js",
  entities: [Generation]
}

query getResult {
  fn: import { getResult } from "@src/generation.js",
  entities: [Generation]
}

query getLatestResults {
  fn: import { getLatestResults } from "@src/generation.js",
  entities: [Generation]
}

query getNumberOfResults {
  fn: import { getNumberOfResults } from "@src/stats.js",
  entities: [Result]
}

job generateResult {
  executor: PgBoss,
  perform: {
    fn:  import { generateResultJob } from "@src/jobs.js",
    executorOptions: {
      pgBoss: {=json { "retryLimit": 1 } json=}
    }
  },
  entities: [Generation],
}

entity Generation {=psl
  id String @id @unique @default(uuid())
  prompt String
  options String @default("")
  status String @default("pending")
  retryTimes Int @default(0)
  createdAt DateTime @default(now())
  resultId String?
  result Result?
psl=}

entity Result {=psl
  id String @id @unique @default(uuid())
  description String
  searchQuery String
  images Image[]
  generationId String @unique
  generation Generation @relation(fields: [generationId], references: [id], onDelete: Cascade)
psl=}

entity Image {=psl
  id String @id @unique @default(uuid())
  url String
  downloadUrl String @default("")
  providerId String
  provider String
  authorId String
  author ImageAuthor @relation(fields: [authorId], references: [id])
  resultId String
  result Result @relation(fields: [resultId], references: [id])
psl=}

entity ImageAuthor {=psl
  id String @id @unique @default(uuid())
  name String
  url String
  images Image[]
psl=}
